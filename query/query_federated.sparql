PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX vg: <http://biohackathon.org/resource/vg#>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX fhir: <http://hl7.org/fhir/>

SELECT
  ?node
(AVG(?osteoPrecence) AS ?avg_osteo)
WHERE {
  {
SELECT
   ?path
   ?osteoPrecence
WHERE {
   SERVICE <http://localhost:3030/Patient_FHIR> {
    	{
       		SELECT
				?sample
      			(MAX(?osteo) AS ?osteoPrecence)
			WHERE {
     			?patient fhir:Patient.identifier [ fhir:Identifier.value [ fhir:value ?pat_id ] ] .
  				OPTIONAL {
    				?con fhir:Condition.code [ fhir:CodeableConcept.text [ fhir:value "Osteroporose"  ] ] .
    				?con fhir:Condition.subject [ fhir:Reference.reference [ fhir:value ?pat ] ] .
    				FILTER(SUBSTR(?pat, 9) = ?pat_id)
  				}
  				BIND(xsd:integer(BOUND(?con)) AS ?osteo)
  				?patient fhir:sampleName ?sample .
			} GROUP BY ?sample
   		}
	}
   ?path rdf:type vg:Path .
   FILTER( CONTAINS( STR(?path), ?sample ) ) .
}
  }
  ?step vg:path ?path .
  ?step vg:node ?node .
}
GROUP BY ?node
HAVING (AVG(?osteoPrecence) = 1)
